apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-allopen"
apply plugin: "realm-android"

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.1"
    defaultConfig {
        applicationId "com.svprdga.infinitescrollsample"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 4
        versionName "1.2.0"
        testInstrumentationRunner "com.svprdga.infinitescrollsample.uitest.custom.CustomTestRunner"

        buildConfigField("String", "IMDB_API_KEY", apikeyProperties["IMDB_API_KEY"])
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        animationsDisabled = true
    }
}

allOpen {
    annotation("com.svprdga.infinitescrollsample.domain.Mockable")
}

ext {
    retrofitVersion = "2.6.2"
    okHttpVersion = "4.2.1"
    glideVersion = "4.9.0"
    androidXNavigation = "2.1.0"
    mockitoVersion = "2.28.2"
    coreVersion = "1.2.0-beta01"
    espressoVersion = "3.2.0"
    hierarchicalContextRunnerVersion = "4.12.1"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Kotlin.
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX & support libraries.
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.navigation:navigation-fragment:$androidXNavigation"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidXNavigation"
    implementation "androidx.navigation:navigation-ui:$androidXNavigation"
    implementation "androidx.navigation:navigation-ui-ktx:$androidXNavigation"
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
    implementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"

    // DI.
    implementation "org.koin:koin-android:2.0.1"

    // RX.
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    // Network.
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Other.
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation ("com.github.bumptech.glide:glide:$glideVersion@aar") {
        transitive = true
    }

    // Unit test.
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation "de.bechte.junit:junit-hierarchicalcontextrunner:$hierarchicalContextRunnerVersion"

    // Instrumentation tests.
    androidTestImplementation "androidx.test:core:$coreVersion"
    androidTestImplementation "androidx.test:core-ktx:$coreVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.1"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "de.bechte.junit:junit-hierarchicalcontextrunner:$hierarchicalContextRunnerVersion"

}
